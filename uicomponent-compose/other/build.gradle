import io.github.droidkaigi.feeder.AppVersions
import io.github.droidkaigi.feeder.Dep
import io.github.droidkaigi.feeder.Versions
// TODO: make this library Kotlin MPP
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'app.cash.exhaustive'
}

apply from: rootProject.file("gradle/android.gradle")

android {
    defaultConfig {
        resValue "string", "app_version_name", AppVersions.versionName
    }
    compileOptions {
        coreLibraryDesugaringEnabled true // need for test. https://github.com/DroidKaigi/conference-app-2021/issues/373
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=androidx.compose.material.ExperimentalMaterialApi"
        freeCompilerArgs += "-Xopt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${Versions.compose}"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xallow-jvm-ir-dependencies"]
        freeCompilerArgs += "-Xopt-in=kotlin.contracts.ExperimentalContracts"
    }
}

dependencies {
    api project(":uicomponent-compose:core")
    api project(":model")

    // Java 8+ API desugaring support
    coreLibraryDesugaring Dep.desugarJdkLibs

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation Dep.Accompanist.coil
    implementation Dep.Accompanist.insets

    implementation (Dep.Coroutines.core) {
        version {
            strictly Versions.coroutines
        }
    }

    // Write here to get from JetNews
    // https://github.com/android/compose-samples/blob/master/JetNews/app/build.gradle#L66
    implementation Dep.Compose.navigation
    implementation Dep.Compose.runtime
    implementation Dep.Compose.livedata
    implementation Dep.Compose.foundation
    implementation Dep.Compose.layout
    implementation Dep.Compose.ui
    implementation Dep.Compose.tooling
    implementation Dep.Compose.material
    implementation Dep.Compose.iconsExtended
    implementation Dep.Compose.animation
    implementation Dep.Compose.constraintLayout

    // Android
    implementation Dep.Jetpack.browser
    implementation Dep.playServicesOssLicenses
}
